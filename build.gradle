import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version "${springDependencyVersion}" apply false
    id 'com.google.cloud.tools.jib' version "${jibVersion}" apply false
    id 'java'
}

subprojects {

    group = 'org.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'java-library'
    apply plugin: 'com.google.cloud.tools.jib'

    jib.to.tags = ["latest"]

    repositories {
        mavenCentral()
    }

    dependencyManagement {

        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.0'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        runtimeOnly 'com.h2database:h2'

        compileOnly 'org.projectlombok:lombok:1.18.20'
        annotationProcessor 'org.projectlombok:lombok:1.18.20'
    }

    test {
        useJUnitPlatform()
    }
}